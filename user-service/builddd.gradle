plugins {
	id 'java'
	id 'idea'
	id 'org.springframework.boot' version '2.3.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.10.RELEASE'
	id 'com.google.cloud.tools.jib' version '2.6.0'
}

group = 'io.services'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
	mavenLocal()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-r2dbc'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.security:spring-security-oauth2-resource-server'
	implementation 'org.springframework.security:spring-security-oauth2-jose'
	implementation 'io.r2dbc:r2dbc-postgresql:0.8.4.RELEASE'
	implementation 'io.r2dbc:r2dbc-pool:0.8.4.RELEASE'

	// Spring Doc
	implementation 'org.springdoc:springdoc-openapi-webflux-core:1.4.8'
	implementation 'org.springdoc:springdoc-openapi-security:1.4.8'

	// other modules
	implementation 'io.services:service-oidc-autoconfigure:0.0.1-SNAPSHOT'


	// immutable/functional/reactive
	implementation("net.javacrumbs.future-converter:future-converter-java8-guava:1.2.0")
	implementation("io.reactivex.rxjava2:rxjava:2.2.6")
	implementation("com.oath.cyclops:cyclops:10.1.1")
	implementation("org.immutables:gson:2.8.2")
	compileOnly("org.immutables:value:2.8.2")
	annotationProcessor("org.immutables:value:2.8.2")

	// Tools
	implementation 'io.projectreactor:reactor-tools'
	compileOnly 'org.projectlombok:lombok'
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation "com.h2database:h2"
}

test {
	useJUnitPlatform()
}

jib {

	to {
		tags = ["${project.version}"]
	}
}

// Workaround for https://github.com/gradle/gradle/issues/4956
sourceSets.configureEach { sourceSet ->
	tasks.named(sourceSet.compileJavaTaskName).configure {
		options.annotationProcessorGeneratedSourcesDirectory = file("$buildDir/generated/sources/annotationProcessor/java/${sourceSet.name}")
	}
}

// Workaround for https://youtrack.jetbrains.com/issue/IDEA-182577
idea {
	module {
		sourceDirs += compileJava.options.annotationProcessorGeneratedSourcesDirectory
		generatedSourceDirs += compileJava.options.annotationProcessorGeneratedSourcesDirectory
		testSourceDirs += compileTestJava.options.annotationProcessorGeneratedSourcesDirectory
		generatedSourceDirs += compileTestJava.options.annotationProcessorGeneratedSourcesDirectory
	}
}

